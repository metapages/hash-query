###############################################################
# Minimal commands to develop, build, test, and deploy
###############################################################
# just docs: https://github.com/casey/just
set shell                          := ["bash", "-c"]
set dotenv-load                    := true
# Change this to anything else to NOT publish a seperate npm module
NPM_PUBLISH                        := "true"
# E.g. 'my.app.com'. Some services e.g. auth need know the external endpoint for example OAuth
# The root domain for this app, serving index.html
export APP_FQDN                    := env_var_or_default("APP_FQDN", "metaframe1.dev")
export APP_PORT                    := env_var_or_default("APP_PORT", "443")
ROOT                               := env_var_or_default("GITHUB_WORKSPACE", `git rev-parse --show-toplevel`)
export CI                          := env_var_or_default("CI", "")
PACKAGE_NAME_SHORT                 := file_name(`cat package.json | jq -r '.name' | sd '.*/' ''`)
# Store the CI/dev docker image in github
export DOCKER_IMAGE_PREFIX         := "ghcr.io/metapages/" + PACKAGE_NAME_SHORT
# Always assume our current cloud ops image is versioned to the exact same app images we deploy
export DOCKER_TAG                  := `if [ "${GITHUB_ACTIONS}" = "true" ]; then echo "${GITHUB_SHA}"; else echo "$(git rev-parse --short=8 HEAD)"; fi`
# The NPM_TOKEN is required for publishing to https://www.npmjs.com
NPM_TOKEN                          := env_var_or_default("NPM_TOKEN", "")
vite                               := "VITE_APP_FQDN=" + APP_FQDN + " VITE_APP_PORT=" + APP_PORT + " NODE_OPTIONS='--max_old_space_size=16384' ./node_modules/vite/bin/vite.js"
tsc                                := "./node_modules/typescript/bin/tsc"
# minimal formatting, bold is very useful
bold                               := '\033[1m'
normal                             := '\033[0m'
green                              := "\\e[32m"
yellow                             := "\\e[33m"
blue                               := "\\e[34m"
magenta                            := "\\e[35m"
grey                               := "\\e[90m"

# If not in docker, get inside
@_help:
    echo -e ""
    just --list --unsorted --list-heading $'üå± Commands:\n\n'
    echo -e ""
    echo -e "    Github  URL üîó {{green}}$(cat package.json | jq -r '.repository.url'){{normal}}"
    echo -e "    Publish URL üîó {{green}}https://www.npmjs.com/package/$(cat package.json | jq -r '.name'){{normal}}"
    echo -e "    Develop URL üîó {{green}}https://{{APP_FQDN}}:{{APP_PORT}}/{{normal}}"
    echo -e ""

# Run the dev server. Opens the web app in browser.
@dev: _dev
    open https://${APP_FQDN}:${APP_PORT};
    just _dev

# Compile check typescript
check: (_tsc "--build")

_dev: _mkcert _ensure_npm_modules (_tsc "--build")
    #!/usr/bin/env bash
    set -euo pipefail
    APP_ORIGIN=https://${APP_FQDN}:${APP_PORT}
    echo "Browser development pointing to: ${APP_ORIGIN}"
    VITE_APP_ORIGIN=${APP_ORIGIN} {{vite}} --clearScreen false

# Build the browser client static assets and npm module
build: _npm_build

# Test: currently bare minimum: only building. Need proper test harness.
@test: _npm_build

# Publish to npm and github pages.
publish npmversionargs="patch": _ensureGitPorcelain test (_npm_version npmversionargs) _npm_publish
    @# Push the tags up
    git push origin v$(cat package.json | jq -r '.version')

# NPM commands: build, version, publish
npm command="":
    #!/usr/bin/env bash
    set -euo pipefail

    if [ "{{command}}" = "build" ];
    then
        just _npm_build
    elif [ "{{command}}" = "version" ];
    then
        just _npm_version
    elif [ "{{command}}" = "publish" ];
    then
        just _npm_publish
    else
        echo ""
        echo "üëâ just npm [ build | version | publish ]"
        echo ""
    fi

# Deletes: .certs dist
clean:
    rm -rf .certs dist

# Rebuild the client on changes, but do not serve
watch BUILD_SUB_DIR="":
    watchexec -w src -w tsconfig.json -w package.json -w vite.config.ts -- just _npm_build

# Watch and serve browser client. Can't use vite to serve: https://github.com/vitejs/vite/issues/2754
serve BUILD_SUB_DIR="": (_browser_assets_build BUILD_SUB_DIR)
    cd docs && ../node_modules/http-server/bin/http-server --cors '*' -o {{BUILD_SUB_DIR}} -a {{APP_FQDN}} -p {{APP_PORT}} --ssl --cert ../.certs/{{APP_FQDN}}.pem --key ../.certs/{{APP_FQDN}}-key.pem

# Build npm package for publishing
@_npm_build: _ensure_npm_modules
    if [ "{{NPM_PUBLISH}}" = "true" ]; then \
        just _npm_build_internal; \
    fi

_npm_build_internal:
    mkdir -p dist
    rm -rf dist/*
    {{tsc}}  src/lib/index.ts --declaration --emitDeclarationOnly --jsx react --esModuleInterop --outDir dist
    {{vite}} build --mode=production
    @# {{tsc}} --noEmit false --project ./tsconfig.npm.json
    @echo "  ‚úÖ npm build"

# bumps version, commits change, git tags
_npm_version npmversionargs="patch":
    npm version {{npmversionargs}}

# If the npm version does not exist, publish the module
_npm_publish: _require_NPM_TOKEN _npm_build
    #!/usr/bin/env bash
    if [ "{{NPM_PUBLISH}}" != "true" ]; then
        exit 0
    fi
    set -euo pipefail
    if [ "$CI" != "true" ]; then
        # This check is here to prevent publishing if there are uncommitted changes, but this check does not work in CI environments
        # because it starts as a clean checkout and git is not installed and it is not a full checkout, just the tip
        if [[ $(git status --short) != '' ]]; then
            git status
            echo -e 'üí• Cannot publish with uncommitted changes'
            exit 2
        fi
    fi

    PACKAGE_EXISTS=true
    if npm search $(cat package.json | jq -r .name) | grep -q  "No matches found"; then
        echo -e "  üëâ new npm module !"
        PACKAGE_EXISTS=false
    fi
    VERSION=$(cat package.json | jq -r '.version')
    if [ $PACKAGE_EXISTS = "true" ]; then
        INDEX=$(npm view $(cat package.json | jq -r .name) versions --json | jq "index( \"$VERSION\" )")
        if [ "$INDEX" != "null" ]; then
            echo -e '  üå≥ Version exists, not publishing'
            exit 0
        fi
    fi

    echo -e "  üëâ PUBLISHING npm version $VERSION"
    if [ ! -f .npmrc ]; then
        echo "//registry.npmjs.org/:_authToken=${NPM_TOKEN}" > .npmrc
    fi
    npm publish --access public .

# build production brower assets
_browser_assets_build BUILD_SUB_DIR="": _ensure_npm_modules
    mkdir -p docs/{{BUILD_SUB_DIR}}
    find docs/{{BUILD_SUB_DIR}} -maxdepth 1 -type f -exec rm "{}" \;
    rm -rf $(echo "docs/{{BUILD_SUB_DIR}}/assets" | sed s#//*#/#g)
    BUILD_SUB_DIR={{BUILD_SUB_DIR}} {{vite}} build --mode=production

# compile typescript src, may or may not emit artifacts
_tsc +args="": _ensure_npm_modules
    {{tsc}} {{args}}

# DEV: generate TLS certs for HTTPS over localhost https://blog.filippo.io/mkcert-valid-https-certificates-for-localhost/
_mkcert:
    #!/usr/bin/env bash
    if ! command -v mkcert &> /dev/null; then echo "üí• {{bold}}mkcert{{normal}}üí• is not installed (manual.md#host-requirements): https://github.com/FiloSottile/mkcert"; exit 1; fi
    if [ ! -f .certs/{{APP_FQDN}}-key.pem ]; then
        mkdir -p .certs/ ;
        cd .certs/ && mkcert -cert-file {{APP_FQDN}}.pem -key-file {{APP_FQDN}}-key.pem {{APP_FQDN}} localhost ;
    fi
    if ! cat /etc/hosts | grep "{{APP_FQDN}}" &> /dev/null; then
        echo -e "";
        echo -e "üí• Add below to /etc/hosts with this command: {{bold}}sudo vi /etc/hosts{{normal}} üí•";
        echo -e "";
        echo -e "{{bold}}127.0.0.1       {{APP_FQDN}}{{normal}}";
        echo -e "";
        exit 1;
    fi
    echo -e "‚úÖ Local mkcert certificates and /etc/hosts contains: 127.0.0.1       {{APP_FQDN}}"

@_ensure_npm_modules:
    if [ ! -f "{{tsc}}" ]; then npm i; fi

# vite builder commands
@_vite +args="":
    {{vite}} {{args}}

@_ensureGitPorcelain:
    if [ ! -z "$(git status --untracked-files=no --porcelain)" ]; then \
        echo -e " ‚ùó Uncommitted files:"; \
        git status --untracked-files=no --porcelain; \
        exit 1; \
    fi

@_require_NPM_TOKEN:
	if [ -z "{{NPM_TOKEN}}" ]; then echo "Missing NPM_TOKEN env var"; exit 1; fi
